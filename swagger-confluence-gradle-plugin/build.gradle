buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.maven:maven-model:3.3.9'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.3"
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "com.gradle.plugin-publish"

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_PASS"
    publications = ['myPublication']
    pkg {
        version {
            name = '1.0-RELEASE'
        }
        repo = "$System.env.BINTRAY_REPO"
        name = 'swagger-confluence-gradle-plugin'
        userOrg = "$System.env.BINTRAY_USER"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://cloud.slkdev.net/gitlab/starlightknight/swagger-confluence'
        websiteUrl = 'https://cloud.slkdev.net/swagger-confluence'
        issueTrackerUrl = 'https://cloud.slkdev.net/gitlab/starlightknight/swagger-confluence/issues'
        labels = ['swagger','confluence']
    }
}

evaluationDependsOn(':swagger-confluence-core')

dependencies {
    compile (project(':swagger-confluence-core'))
    compile gradleApi()
}

publishing {
    def mavenReleaseUrl = "$System.env.MAVEN_PUBLISH_PLUGIN_RELEASE_URL"
    def mavenSnapshotUrl = "$System.env.MAVEN_PUBLISH_PLUGIN_SNAPSHOT_URL"
    def mavenUser = "$System.env.MAVEN_PUBLISH_USER"
    def mavenPass = "$System.env.MAVEN_PUBLISH_PASS"

    repositories {
        maven {
            if (mavenReleaseUrl != "null" || mavenSnapshotUrl != "null") {
                if (mavenSnapshotUrl != "null" && project.version.endsWith('-SNAPSHOT')) {
                    url mavenSnapshotUrl
                }
                else {
                    url mavenReleaseUrl
                }
                credentials {
                    username mavenUser
                    password mavenPass
                }
            }
            else {
                url "${System.properties.'user.home'}/.m2/repository"
            }
        }
    }
}

publishing {
	publications {
		myPublication(MavenPublication) {
			from components.java
		}
	}
}

pluginBundle {
    website = 'https://cloud.slkdev.net/swagger-confluence/'
    vcsUrl = 'https://cloud.slkdev.net/gitlab/starlightknight/swagger-confluence'
    description = 'A plugin for parsing a Swagger schema and publishing API documentation to an Atlassian Confluence server'
    tags = ['swagger', 'confluence']

    plugins {
        swaggerConfluencePlugin {
            id = 'net.slkdev.swagger-confluence'
            displayName = 'Swagger Confluence'
        }
    }

    mavenCoordinates {
        groupId = "net.slkdev.swagger.confluence"
        artifactId = "swagger-confluence-gradle-plugin"
    }
}